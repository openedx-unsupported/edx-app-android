import org.ajoberstar.grgit.*
import org.ajoberstar.grgit.exception.*

buildscript {
    dependencies {
        classpath 'org.ajoberstar:grgit:1.0.0'
    }
    repositories {
        mavenCentral()
    }
}

def edXAppPluginSetup(url, ref) {
    def buildSrc = new File(rootProject.projectDir, 'plugins')
    buildSrc.mkdirs()
    def dest = new File(buildSrc, "edx-app-gradle-plugin")

    if(!dest.exists()) {
        Grgit.clone(dir: dest, uri: url, refToCheckout : ref)
    }

    def grgit = Grgit.open(dir : dest)
    try {
        grgit.fetch()
    }
    catch(GrgitException e) {
        println("Could not fetch latest version of app gradle plugin. This is probably due to lack of an internet connection. Skipping")
    }
    grgit.checkout(branch : ref)
}

edXAppPluginSetup('https://github.com/edx/edx-app-gradle-plugin', '2e0516c8b3c4e0d60bff01491bc66c9f0f2eedf4')

include "plugins/edx-app-gradle-plugin"
